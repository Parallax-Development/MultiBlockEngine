plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.3.0'
    id 'maven-publish'
}

base {
    archivesName = rootProject.name
}

dependencies {
    implementation project(":core-api")

    compileOnly "io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT"
    compileOnly 'me.clip:placeholderapi:2.11.5'

    implementation 'com.google.code.gson:gson:2.10.1'
    implementation "com.zaxxer:HikariCP:5.1.0"

    shadow "com.zaxxer:HikariCP:5.1.0"

    testImplementation platform('org.junit:junit-bom:5.10.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.20:3.93.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

tasks.named('jar') {
    enabled = false
}

tasks.named('sourcesJar') {
    enabled = false
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveClassifier.set('')
    archiveBaseName.set(rootProject.name)
    archiveVersion.set(project.version.toString())
    destinationDirectory.set(rootProject.layout.buildDirectory.dir('libs'))
    relocate 'com.zaxxer.hikari', 'com.darkbladedev.engine.libs.hikari'
    relocate 'com.google.gson', 'com.darkbladedev.engine.libs.gson'
}

build.dependsOn shadowJar

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mbeCore(MavenPublication) {
            artifactId = 'mbe-core'
            artifact(tasks.shadowJar)
            artifact(tasks.sourcesJar)
        }
    }
    repositories {
        mavenLocal()
        def repo = System.getenv('GITHUB_REPOSITORY')
        if (repo != null && !repo.isBlank()) {
            maven {
                name = 'GitHubPackages'
                url = uri('https://maven.pkg.github.com/' + repo)
                credentials {
                    username = System.getenv('GITHUB_ACTOR')
                    password = System.getenv('GITHUB_TOKEN')
                }
            }
        }
    }
}
